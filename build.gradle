plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version "6.25.0"
    id 'com.github.spotbugs' version '6.0.12'
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'checkstyle'
}

group = 'pe.com.graduate.insights.api'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    google()
}

jar {
    enabled = false
}

bootJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}

dependencies {
    //spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    //mail
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.3.0'

    //mysql
    implementation 'com.mysql:mysql-connector-j:8.3.0'

    //mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    //lombok
    implementation 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    //spotbugs
    implementation 'com.google.code.findbugs:annotations:3.0.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'

    //Poi
    implementation 'org.apache.poi:poi-ooxml:5.0.0'

    //swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.5.0'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

task printVersion {
    doLast {
        println version
    }
}

apply from: "$rootDir/gradle/static-analysis.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"