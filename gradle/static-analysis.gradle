
apply plugin: 'checkstyle'
apply plugin: 'pmd'

checkstyle {
    toolVersion = '10.3.1'
    configFile = file("${rootDir}/config/checkstyle/google_checks.xml")
}

tasks.register('checkstyle') {
    dependsOn project.tasks.findAll { task -> task.name.matches('checkstyle\\w+') }
}

tasks.register('pmd') {
    dependsOn project.tasks.findAll { task -> task.name.matches('pmd\\w+') }
}



pmd {
    toolVersion = '6.13.0'
    ignoreFailures = false
    pmdTest.enabled = false
    ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
    ruleSets = []
}

spotbugs {
    ignoreFailures.set(true)
    showProgress.set(true)
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'

        }
    }
}

// format configuration
spotless {
    encoding 'UTF-8'

    java {
        removeUnusedImports()
        toggleOffOn('formatter:off', 'formatter:on')
        googleJavaFormat()
    }
}

tasks.withType(Checkstyle).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

tasks.withType(Pmd).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

tasks.named('check').configure {
    dependsOn tasks.named('checkstyle')
    dependsOn tasks.named('jacocoTestCoverageVerification')
}